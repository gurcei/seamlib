' ==================
' 'SPRING' BOOT MENU
' ==================

#output "autoboot.c65"


'--------
.declares
'--------
#declare rom$, vm$, fname$
#declare mask%, month%, year%, ds%, date$
#declare fpga_hash$, reverse%, upper, lower
#declare a$, k, offs, xemu_flag
#declare rbw_clr, ctime, ret%
#declare line_cnt, ch, skip_flag
#declare core_year%, core_month%, core_day%


'-------
.defines
'-------
#define TRUE = 1
#define FALSE = 0
#define LINEFEED = 10
#define RETURN = 13
#define KEY_RETURN = chr$(13)
#define KEY_ESC = chr$(27)


'----
.init
'----
  fast 40
  bank 128
  offs = $ffd3630
  poke $d020, 0, 0
  print KEY_ESC;"8"
  'print chr$(142);  ' switch back to upper-case
  print chr$(14);  ' lower-case


'----
.main
'----
  gosub check_if_reboot_after_rom_switch

  gosub show_main_menu

  ctime=ti
  do
    if ti-ctime > .03 then begin
      gosub cycle_colours
      ctime=ti
    bend
    get a$
  loop while a$=""

  if a$="1" then gosub perform_checks
  if a$="2" then gosub show_about
  if a$="3" then gosub show_source_list
  if a$="/" then print "{x93}bye!";:end
  
  goto main

  end


'--------------------------------
.check_if_reboot_after_rom_switch
'--------------------------------
  if peek($40000) = asc("g") and peek($40001) = asc("i") {x5F}
      and peek($40002) = asc("b") then begin
    dload "seamlib"
  bend
  return


'-------------
.cycle_colours
'-------------
  for k = 0 to 5
    poke $ff80001+k, mod(rbw_clr+k,16) + 64
  next k
  rbw_clr = mod(rbw_clr + 1, 16)
  return


'--------------
.show_main_menu
'--------------
  print "{x93}{x5}";
  print "'{x1}{x90}S{x5}p{x1C}r{x9F}i";chr$(156);"n{x1E}g{x5}{x4}' -  {x9A}A game by Moana (and her daddy){x5}  -  20th August 2025"
  print "========"
  print
  print "{x9F}1{x5}) Play game"
  print "{x9F}2{x5}) About this game"
  print "{x9F}3{x5}) Browse the source code"
  print "{x9F}/{x5}) Exit"

  return


'--------------
.perform_checks
'--------------
  ' SPRING game works fine in xemu, so not a big worry there
  gosub xemu_check

  if xemu_flag = 0 then begin
    gosub core_check
  bend

  gosub rom_check

  gosub ntsc_to_pal

  clr
  dload "seamlib"
  return


'------------------
.is_core_date_older
'------------------
  if year% < core_year% then ret% = TRUE : return
  if year% > core_year% then ret% = FALSE : return

  if month% < core_month% then ret% = TRUE : return
  if month% > core_month% then ret% = FALSE : return

  if ds% < core_day% then ret% = TRUE : return
  if ds% > core_day% then ret% = FALSE : return

  ' final case of dates being equal
  ret% = FALSE
  return


'----------
.core_check
'----------
  gosub format_datestamp

  gosub format_fpga_hash

  core_year% = 2025
  core_month% = 4
  core_day% = 21

  gosub is_core_date_older

  if fpga_hash$<>"aaf4542d" or ret% = TRUE then begin
    print "{x93}{x11}{x11}{x1C}CORE WARNING{x5}: This game has been tested against this core:"
    print "- 0.97 Release core: {x1E}aaf4542d  2025-04-22{x5}"
    print "-         Your core: {x1C}";fpga_hash$;"  ";date$;"{x5}"
    print
    print "If experiencing problems, please download this core from the filehost:"
    print "- {x9F}https://files.mega65.org{x5}"
    print "  - search for 'core release' and choose your download target (r2, r3, r6)"
    print
    print "- Unzip the '{x9F}mega65*.7z{x5}' file and locate the .cor file within"
    print "- Place it on your sd-card"
    print "- Power up with the '{x9F}no scroll{x5}' key and flash it to a spare slot"
    print "- Select this core from the flash menu to run it"
    print
    print "{x9E}Press any key to continue...{x5}"
    get key a$
  bend

  return


'----------
.show_about
'----------
  print "{x93}{x96}ABOUT{x5} (1/3)"
  print  "====="
  print "My daddy kept bugging me to write a game on his silly MEGA65 thing."
  print "I finally relented and shared my game design vision with him on the"
  print "condition that he do all the dumb code monkey work and leave all the"
  print "important game design work to me, so that I have top billing in the"
  print "credits, and I might consider giving him a little mention in passing."
  print "Either that or I throw a few peanuts his way as a reward ;)"

  print "{x11}{x9E}Press any key to continue...{x5}"
  get key a$

  print "{x93}{x96}ABOUT{x5} (2/3)"
  print  "====="

  print "Ok, daddy's turn now. I thought it was time to finally bite the bullet"
  print "and write a SEAM/RRB-based game in Eleven + BASIC 65."

  print

  print "It was challenging, required some assembly support routines to speed"
  print "things up, but I'm happy we got to there."

  print

  print "On the whole, it's a simple, cute game, suitable for young children to"
  print "improve their hand-eye coordination with a joystick. I like to think of it"
  print "as a game that Mastertronics would have deemed worthy to release back in"
  print "those days! :)"
  print
  print " Some fun technical aspects of it were that:"
  print "- I had to write a few tools from scratch:"
  print "  - A palette editor called 'PALETTE' (in Eleven/BASIC)"
  print "  - A SEAM-FCM sprite drawing tool called 'SEAMDRAW' (in Eleven/BASIC)"
  print "  - The 'SEAMLIB' library (with ACME Assembler on PC)"
  print "- The game is in 640x400 resolution"
  print "- The game presents about 13 sprites on screen in the difficult level"

  print "{x11}{x9E}Press any key to continue...{x5}"
  get key a$

  print "{x93}{x96}ABOUT{x5} (3/3)"
  print  "====="

  print "The in-game music was my cover of the theme song from a very old and" 
  print "forgotten C64 game called 'Dungeon of the Algebra Dragons'."
  print "See this cover as my attempt to pay homage to this early era of"
  print "computer gaming. Sure, games looked crude and simple back then, but"
  print "they still had their own charm!"
  print
  print "Also, the high score music is my cover of another obscure C64 game"
  print "called 'Flight Path 737' ;)"
  print
  print "...and yes, all music is performed by {x9F}BASIC 65's 'PLAY'{x5} command! ;)"

  print

  print "For the MEGA65 community:"
  print "I feel like this is our 1983-era, us devs are skilling up and sharing"
  print "our humble creations. As time rolls on, our skills will grow and our"
  print "projects will get more ambitious, showcasing more and more of"
  print "what the MEGA65 system is capable of."

  print "So here's looking forward to the years ahead of us!"

  print "{x11}{x9E}Press any key to continue...{x5}"
  get key a$

  a$=""
  return


'----------------
.show_source_list
'----------------
  print "{x93}{x96}MAIN SOURCE FILES{x5}"
  print "================="
  print "{x9F}1{x5}) seamlib.el"
  print "{x9F}2{x5}) palette.el"
  print "{x9F}3{x5}) seamdraw.el"
  print "{x9F}4{x5}) asmhelper.a"
  print
  print "{x96}MISC. FILES{x5}"
  print "==========="
  print "{x9F}5{x5}) melody.el"
  print "{x9F}6{x5}) autoboot.el"
  print
  print "{x9F}/{x5}) Exit"
  get key a$

  if a$="1" then gosub seamlib_el
  if a$="2" then gosub palette_el
  if a$="3" then gosub seamdraw_el
  if a$="4" then gosub asmhelper_a

  if a$="5" then gosub melody_el
  if a$="6" then gosub autoboot_el

  if a$="/" then a$="": return

  goto show_source_list
  return


.autoload_11
'-----------
  print "{x93}autoload ";chr$(34);fname$;chr$(34):clr:dload "11boot.c65"
  return


.autoload_ma
'-----------
  poke $d020,6,6
  print chr$(142);  ' switch back to upper-case
  print "{x93}autoload ";chr$(34);fname$;chr$(34)
  clr
  dload "mega assembler"
  return


.seamlib_el
'----------
  do
    print "{x93}";
    print "{x96}seamlib.el{x5}"
    print "=========="
    print "This started off as an example of using the seamlib library, but then"
    print "evolved into the actual 'Spring' game, so I probably should have renamed"
    print "it to be called 'spring.el'..."
    print
    print "This file is worth a browse for folks curious to see how the seamlib"
    print "can be made use of."
    print
    print "You're also able to modify and re-build the game to your liking, as a"
    print "copy of the Eleven IDE is within this disk also."
    print
    print "{x9F}RETURN{x5}) Open '{x96}seamlib.el{x5}' in Eleven"
    print "     {x9F}/{x5}) Exit"

    get key a$
    if a$=chr$(13) then fname$="seamlib.el":goto autoload_11
  loop while a$<>"/"
  a$=""
  return


.palette_el
'----------
  do
    print "{x93}";
    print "{x96}palette.el{x5}"
    print "=========="
    print "I wanted a tool to help me design a palette for my game, allowing for"
    print "easy creation of gradients."
    print
    print "Alas, it doesn't have any in-built help system yet, but you can browse"
    print "the source, searching for the 'user_input' routine, to get a sense of"
    print "what it is capable of."
    print "  - F1 = load palette file (e.g. try loading 'game.pal')"
    print "  - F3 = save palette file"
    print
    print "I'll endeavour to add an in-built help system in future..."
    print
    print "{x9F}RETURN{x5}) Open '{x96}palette.el{x5}' in Eleven"
    print " {x9F}SPACE{x5}) Run '{x96}PALETTE{x5}' (compiled source)"
    print "     {x9F}/{x5}) Exit"

    get key a$
    if a$=chr$(13) then fname$="palette.el":goto autoload_11
    if a$=" " then clr:dload "palette"
  loop while a$<>"/"
  a$=""
  return


.seamdraw_el
'----------
  do
    print "{x93}";
    print "{x96}seamdraw.el{x5}"
    print "============"
    print "I made this tool so that I could quickly doodle seam-fcm sprites of"
    print "any custom size I like. It may not be the most efficient use of character"
    print "data, but it gets the job done in a simple, straightforward way."
    print
    print "Again, apologies for the lack of in-built help/documentation, but hopefully"
    print "searching the code for 'user_input' will show you all the available"
    print "capabilities."
    print
    print "  - F1 = load seam-data file (e.g. try loading 'moana4.bin')"
    print "  - F3 = save seam-data file"
    print
    print "{x9F}RETURN{x5}) Open '{x96}seamdraw.el{x5}' in Eleven"
    print " {x9F}SPACE{x5}) Run '{x96}SEAMDRAW{x5}' (compiled source)"
    print "     {x9F}/{x5}) Exit"

    get key a$
    if a$=chr$(13) then fname$="seamdraw.el":goto autoload_11
    if a$=" " then clr:dload "seamdraw"
  loop while a$<>"/"
  a$=""
  return


.asmhelper_a
'-----------
  do
    print "{x93}";
    print "{x96}asmhelper.a{x5}"
    print "==========="
    print "I really ought to rename this file to 'seamlib.a'..."
    print
    print "It is built via the {x9A}Acme{x5} compiler. My style of assembly coding has"
    print "evolved to become very macro-intensive, as my eyes get more and more"
    print "irritated at looking at large swathes of raw assembly opcodes."
    print
    print "I also found this makes for a saner debugging experience, as while"
    print "debugging in xemu, I can use the 'next' command to step over macros!"
    print
    print "If you prefer viewing the file on your pc, take a look here:"
    print "- https://github.com/gurcei/seamlib/blob/main/asmhelper.a"
    print
    print "This '{x9A}asmhelper.a{x5}' is to be compiled on your PC via the Acme compiler."
    print "I have provided it on this disk just to ease your viewing pleasure!"
    print
    print "{x9F}RETURN{x5}) Show '{x96}asmhelper.a{x5}' listing"
    print "     {x9F}/{x5}) Exit"

    get key a$
    if a$=chr$(13) then begin
      fname$ = "asmhelper.a"
      gosub show_ascii
      
^^    font c
      print chr$(14);  ' lower-case
    bend

  loop while a$<>"/"
  a$=""
  return


'----------
.show_ascii
'----------
  dopen #2, (fname$), r, u8

  print chr$(147);
  
^^  font a
    print chr$(14);  ' lower-case

  line_cnt = 0
  do
    get#2, ch
    if st then begin
      dclose #2
      print "<<< END OF FILE >>> - press RETURN key"
      do
        get key a$
      loop while a$ <> KEY_RETURN
      return
    bend

    skip_flag = 0
    ' ascii-lowercase to petscii-lowercase
    if ch >= 97 and ch <= 122 then begin
      ch = ch - 32
      skip_flag = 1
    bend

    ' ascii-uppercase to petscii uppercase
    if skip_flag = 0 and ch >= 65 and ch <= 90 then begin
      ch = ch + 128
      skip_flag = 1
    bend

    ' look for linux /n eol char, and consider it to be return
    if skip_flag = 0 and ch = LINEFEED then begin
      ch = RETURN
      line_cnt = line_cnt + 1
      skip_flag = 1
    bend

    ' ascii underscore to petscii underscore (mega+p)
    if skip_flag = 0 and ch = asc("{x5F}") then begin
      ch = $af
      skip_flag = 1
    bend

    ' ascii-caret to petscii up-arrow?
    if skip_flag = 0 and ch = 94 then begin
      ch = asc("^")
      skip_flag = 1
    bend

    print chr$(ch);

    if line_cnt = 23 then begin
      print "{x9F}<<< MORE >>> Q = quit, any other key to show next page{x5}";
      print KEY_ESC;"j";

      get key a$
      if a$ = "q" then dclose #2 : return
      line_cnt = 0
      print KEY_ESC;"q";
    bend
  loop

  dclose #2

  return


.melody_el
'---------
  do
    print "{x93}";
    print "{x96}melody.el{x5}"
    print "========="
    print "This is the same tool I used back in 'The Silent Enigma' demo, to sequence"
    print "melody strings to suit BASIC 65's PLAY command. I don't think it has"
    print "evolved a great deal since last time. It's a quick and dirty tool that"
    print "deserves more love to make it more aesthetically pleasing, but for now"
    print "it does its job well enough."
    print
    print "Usage:"
    print "- Press {x9A}F5{x5} to build from source"
    print "- Press {x9A}F1{x5} to load a song (try loading '{x9A}dragon.p{x5}')"
    print "- Press {x9A}F9{x5} to open sequencer"
    print "- Press {x9A}Shift+Return{x5} to play sequence"
    print
    print "{x9F}RETURN{x5}) Open '{x96}melody.el{x5}' in Eleven"
    print " {x9F}SPACE{x5}) Run '{x96}playmaker{x5}' (compiled source)"
    print "     {x9F}/{x5}) Exit"

    get key a$
    if a$=chr$(13) then fname$="melody.el":goto autoload_11
    if a$=" " then clr:dload "playmaker"
  loop while a$<>"/"
  a$=""
  return


.autoboot_el
'-----------
  do
    print "{x93}";
    print "{x96}autoboot.el{x5}"
    print "==========="
    print "I've recycled 'The Silent Enigma' demo's boot menu and tailored it for"
    print "the 'Spring' game instead."
    print
    print "It was a nice win to be able to re-use the existing core/rom/xemu check"
    print "logic within it, was a good time saver."
    print
    print "This time around, I also added some logic to help switch roms on the fly"
    print
    print "{x9F}RETURN{x5}) Open '{x96}autoboot.el{x5}' in Eleven"
    print "     {x9F}/{x5}) Exit"

    get key a$
    if a$=chr$(13) then fname$="autoboot.el":goto autoload_11
    if a$="/" then a$="":return
    goto autoboot_el
  loop while a$<>"/"
  a$=""
  return


'----------
.xemu_check
'----------
  if (peek($ffd360f) and $20)=0 then xemu_flag = 1

  if xemu_flag = 0 then return

  return

  ' NOTE: I don't need this warning this time around
  print "{x93}{x11}{x11}{x1E}XEMU NOTE{x5}: You are running this demo in the Xemu emulator."
  print
  print "Please note that some aspects of this demo run slightly faster"
  print "under Xemu compared to real hardware (likely due to differences in"
  print "how much time is consumed for dma calls, but still to be investigated)"
  print
  print "Apart from that, it should still run fine."
  print "{x11}{x9E}Press any key to continue...{x5}"
  get key a$
  return


'-----------
.ntsc_to_pal
'-----------
  if (peek($d06f) and 128)=0 then vm$ = "pal":else vm$="ntsc"

  if vm$ = "pal" then return

  print "{x93}{x11}{x11}{x5}This program requires switching from {x1C}NTSC{x5} to {x1E}PAL{x5}"
  print
  print "Will switch over now."

  print "{x11}{x9E}Press any key to continue...{x5}"
  get key a$
  poke $ffd306f,$00
  poke $ffd3072,$00
  poke $ffd3048,$68
  poke $ffd3049,$00 or (peek($ffd3049) and $f0)
  poke $ffd304a,$f8
  poke $ffd304b,$01 or (peek($ffd304b) and $f0)
  poke $ffd304e,$68
  poke $ffd304f,$00 or (peek($ffd304f) and $f0)
  poke $ffd3072,$00
  poke $ffd3c0e, peek($ffd3c0e) or $80
  poke $ffd3d0e, peek($ffd3d0e) or $80

  return


'---------
.rom_check
'---------
  for k = 0 to 5
    rom$=rom$ + chr$(peek($20017+k))
  next k

  if rom$<>"999999" then begin
    print "{x93}{x11}{x11}{x1C}ROM WARNING{x5}: This game presently works best using:"
    print "- Experimental rom: {x1E}v999999{x5}"
    print "-         Your rom: {x1C}v";rom$;"{x5}"
    print
    print "Would you like to temporarily switch to this rom?"
    print
    print "{x1C}If you don't switch, you will experience music slowdown issues due to "
    print "existing/older roms blocking basic 'PLAY' command while in sys calls"
    print
    print "{x5}Future roms will likely repair this matter."
    print
    print "When that occurs, I will provide an updated version of the game on the"
    print "filehost (https://files.mega65.org) to suit the newer rom."
    print
    print "If you experience any problems even after switching to this rom, please"
    print "visit the MEGA65 discord server (https://mega65.org/chat) and send Gurce"
    print "a message, and we can investigate any unusual problems."
    print
    print "- ({x9F}Y{x5})es, please temporarily switch to v999999 experimental rom"
    print "- ({x9F}N{x5})o thanks, I'll stick with my existing rom"

    do while 1
      get key a$
      if a$ = "y" then gosub update_rom
      if a$ = "n" then exit
    loop
  bend
  return


'----------
.update_rom
'----------
  bload "asmhelper"
  bload "999999.bin",p($8000000),r

  ' add my gi-boot marker
  poke $40000, asc("g"), asc("i"), asc("b")

  ' wipe eleven's magic signature
  poke $4ff00, 0, 0

  bank 0 : sys $7f24 : bank 128
  return


'----------------
.format_datestamp
'----------------
' A port of lydon's function in "freeze_megainfo.c"

  mask% = $ff  ' for max10, would need to be $3f

  month% = 1
  year% = 2020
  ds% = ((peek(offs + 1) and mask%) * 256) + peek(offs)

  do while ds% > 366
    year% = year% + 1
    ds% = ds% - 366
  loop

  if month%=1 and ds%>31 then begin
    month% = month% + 1
    ds% = ds% - 31
  bend

  if month%=2 and (not (year% and 3)) and ds%>29 then begin
    month% = month% + 1
    ds% = ds% - 29
  bend

  if month%=2 and (year% and 3) and ds%>28 then begin
    month% = month% + 1
    ds% = ds% - 28
  bend

  if month%=3 and ds%>31 then begin
    month% = month% + 1
    ds% = ds% - 31
  bend

  if month%=4 and ds%>30 then begin
    month% = month% + 1
    ds% = ds% - 30
  bend

  if month%=5 and ds%>31 then begin
    month% = month% + 1
    ds% = ds% - 31
  bend

  if month%=6 and ds%>30 then begin
    month% = month% + 1
    ds% = ds% - 30
  bend

  if month%=7 and ds%>31 then begin
    month% = month% + 1
    ds% = ds% - 31
  bend

  if month%=8 and ds%>31 then begin
    month% = month% + 1
    ds% = ds% - 31
  bend

  if month%=9 and ds%>30 then begin
    month% = month% + 1
    ds% = ds% - 30
  bend

  if month%=10 and ds%>31 then begin
    month% = month% + 1
    ds% = ds% - 31
  bend

  if month%=11 and ds%>30 then begin
    month% = month% + 1
    ds% = ds% - 30
  bend

  date$=mid$(str$(year%),2)

  if month% > 9 then begin
    date$ = date$ + "-"
  bend:else begin
    date$ = date$ + "-0"
  bend

  date$ = date$ + mid$(str$(month%), 2)


  if ds% > 9 then begin
    date$ = date$ + "-"
  bend:else begin
    date$ = date$ + "-0"
  bend

  date$ = date$ + mid$(str$(ds%), 2)

  return


'----------------
.format_fpga_hash
'----------------
' A port of lydon's function in "freeze_megainfo.c"

  reverse% = 0  ' max10 is reversed

  if reverse% then begin
    upper = (peek(offs+2) * 256) + peek(offs+3)
    lower = (peek(offs+4) * 256) + peek(offs+5)
  bend:else:begin
    upper = (peek(offs+5) * 256) + peek(offs+4)
    lower = (peek(offs+3) * 256) + peek(offs+2)
  bend

  fpga_hash$ = hex$(upper) + hex$(lower)

  return
  
ÿ